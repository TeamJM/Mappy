plugins {
    id 'java'

    id 'com.github.johnrengelman.shadow' version '5.2.0'

    id 'org.jetbrains.kotlin.jvm' version '1.3.61'
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.3.61'
}

group 'info.journeymap.mappy'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    jcenter()
    mavenCentral()
}

sourceSets {
    main.java.srcDirs += 'src/main/kotlin/'
    test.java.srcDirs += 'src/test/kotlin/'
}

dependencies {
    shadow 'ch.qos.logback:logback-classic:1.3.0-alpha5'
    shadow 'com.charleskorn.kaml:kaml:0.15.0'
    shadow 'com.jagrosh:jda-utilities:3.0.2'
    shadow 'io.github.microutils:kotlin-logging:1.7.8'
    shadow 'net.dv8tion:JDA:4.0.0_39'
    shadow "org.snakeyaml:snakeyaml-engine:2.0"

    shadow 'org.jetbrains.kotlinx:kotlinx-serialization-runtime:0.14.0'
    shadow 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

jar {
    manifest { attributes "Main-Class": "info.journeymap.mappy.MainKt" }
}

shadowJar {
    configurations = [project.configurations.shadow]

    relocate "org.intellij", "info.journeymap.shaded.org.intellij"
    relocate "org.jetbrains", "info.journeymap.shaded.org.jetbrains"

    relocate "kotlin", "info.journeymap.shaded.kotlin.kotlin"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.java.srcDirs
}

artifacts {
    archives sourcesJar
    archives shadowJar
}