buildscript {
    repositories {
        maven {
            name = 'KotDis'
            url = 'https://maven.kotlindiscord.com/repository/maven-public/'
        }
    }
}

plugins {
    id 'application'

//    id 'com.github.jakemarsden.git-hooks' version '0.0.1'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id 'io.gitlab.arturbosch.detekt' version '1.8.0'
    id 'org.jetbrains.kotlin.jvm' version '1.3.72'
}

group 'info.journeymap.mappy'
version '1.1-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenLocal()

    maven {
        name = 'KotDis'
        url = 'https://maven.kotlindiscord.com/repository/maven-snapshots/'
    }

    mavenCentral()
    jcenter()

    maven {
        name = 'Kord'
        url = 'https://dl.bintray.com/kordlib/Kord'
    }
}

dependencies {
    detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:1.8.0"

    implementation 'com.kotlindiscord.kord.extensions:kord-extensions:1.1-SNAPSHOT'

    implementation 'com.uchuhimo:konf:0.22.1'
    implementation 'com.uchuhimo:konf-toml:0.22.1'
    implementation 'com.uchuhimo:konf-yaml:0.22.1'

    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    implementation 'org.jetbrains.kotlin:kotlin-reflect'

    implementation 'org.nibor.autolink:autolink:0.10.0'

    // Logging dependencies
    implementation 'ch.qos.logback:logback-classic:1.2.3'
    implementation 'io.github.microutils:kotlin-logging:1.7.9'
    implementation 'io.sentry:sentry-logback:1.7.30'
    implementation 'org.codehaus.groovy:groovy:3.0.4'  // For logback config
}

application {
    mainClassName = "info.journeymap.mappy.MainKt"
}

compileKotlin {
    kotlinOptions.jvmTarget = '11'
}

compileTestKotlin {
    kotlinOptions.jvmTarget = '11'
}

detekt {
    buildUponDefaultConfig = true
    config = files("detekt.yml")
}

//gitHooks {
//    hooks = ['pre-commit': 'detekt']
//}

jar {
    manifest {
        attributes(
                "Main-Class": "info.journeymap.mappy.MainKt"
        )
    }
}

processResources {
    dependsOn "generateBuildInfo"

    from "src/main/resources/build.properties"
}

task generateBuildInfo() {
    outputs.files "src/main/resources/build.properties"

    doLast {
        new File("src/main/resources/build.properties").text = """
            version=$project.version
        """.stripIndent().strip()
    }
}
